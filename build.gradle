import gov.nyc.doitt.nyc.gis.gradle.*

plugins {
    id 'base'
    id 'org.hidetake.ssh' version '1.1.2'
}

ext{
	appName = 'nyc-hurricane'
	ver = 'v0.1.9-SNAPSHOT'
	
	jsSrcDir = "build/${ver}/js"
	cssSrcDir = "build/${ver}/css"

	orderedJsFiles = ['nyc.js', 'style.js', 'content.js', 'app.js', 'init.js']
	orderedCssFiles = ['hurricane.css']

	local_nyclib = 'https://maps.nyc.gov/nyc-lib/'
	
	default_env = 'local'
}

task setEnv << {
	if (!project.hasProperty('env')) project.ext.env = default_env
 	archive.archiveName = "hurricane-${ver}-${env}.zip"
	archive.from {"build/${ver}"}
}

task copyFiles(dependsOn: clean) << {
	def build = new File("build/${ver}")
	build.mkdirs()
	copy {
		from 'src/main/webapp'
		include '*.html'
		include '*.json'
		include 'img/**'
		include 'manifest.webmanifest'
		exclude 'img/Thumbs.db'
		include 'css/**'
		include 'data/**'
		include 'js/**'
		include '311/**'
		into "build/${ver}" 
	}
}

task replaceTokens(dependsOn: [setEnv, copyFiles]) << {
	ant.taskdef(name: 'replace', classname: 'org.apache.tools.ant.taskdefs.Replace')	
	if (project.hasProperty("hurricane.${env}.geoclient.url")){
		ant.replace(file: "build/${ver}/js/init.js", token: project.ext['hurricane.git.geoclient.url'], value: project.ext["hurricane.${env}.geoclient.url"])
	}
	if (project.hasProperty("hurricane.${env}.google.url")){
		ant.replace(file: "build/${ver}/js/init.js", token: project.ext['hurricane.local.google.url'], value: project.ext["hurricane.${env}.google.url"])
	}
	if (project.hasProperty('hurricane.analytics')){
		ant.replace(file: "build/${ver}/index.html", token: '/* google analytics */', value: project.ext['hurricane.analytics'])
		ant.replace(file: "build/${ver}/down.html", token: '/* google analytics */', value: project.ext['hurricane.analytics'])
	}

	ant.replace(file: "build/${ver}/index.html", token: 'hurricane.css', value: "${appName}.css?${ver}")
	ant.replace(file: "build/${ver}/index.html", token: '<script src="js/style.js"></script>', value: '')
	ant.replace(file: "build/${ver}/index.html", token: '<script src="js/nyc.js"></script>', value: '')
	ant.replace(file: "build/${ver}/index.html", token: '<script src="js/content.js"></script>', value: '')
	ant.replace(file: "build/${ver}/index.html", token: '<script src="js/app.js"></script>', value: '')
	ant.replace(file: "build/${ver}/index.html", token: 'js/init.js', value: "js/${appName}.js?${ver}")
	ant.replace(file: "build/${ver}/index.html", token: local_nyclib, value: project.ext["hurricane.${env}.nyc-lib"])

	ant.replace(file: "build/${ver}/311/index.html", token: 'hurricane-version', value: ver)
	ant.replace(file: "build/${ver}/311/index.html", token: '../js/', value: '../src/js/')
	

}

task minifyScript(type: MiniJsTask, dependsOn: [replaceTokens]) {
	libName = appName
	version = ver
	fileNames = orderedJsFiles
	sourceDir = jsSrcDir
	destinationDir = "build/${ver}/js" 
}
minifyScript.doLast {
	delete fileTree(dir: "build/${ver}/js", include: '*.js', exclude: "${appName}.*")
}

task minifyCss(type: MiniCssTask, dependsOn: [replaceTokens]) {
	libName = appName
	fileNames = orderedCssFiles
	sourceDir = cssSrcDir
	destinationDir = "build/${ver}/css" 
}
minifyCss.doLast {
	delete fileTree(dir: "build/${ver}/css", include: 'hurricane.css', exclude: "${appName}.*")
}

task buildApp(dependsOn: [minifyScript, minifyCss]) << {}

task archive(type: Zip, dependsOn: [buildApp]) {}

remotes {
	deployTarget {}
}

task deploy(dependsOn: [archive]) << {
	def archiveDir = project.ext['archive.dir']
	def deployDir = project.ext['hurricane.deploy.dir']
	
	remotes.deployTarget.host = project.ext["${env}.host"]
    remotes.deployTarget.user = project.ext["${env}.user"]	
    remotes.deployTarget.identity = file("${System.properties['user.home']}/.ssh/id_rsa")

	println "deploying ${archive.archiveName} to ${remotes.deployTarget.host}:${deployDir}"
	
	ssh.run {
        session(remotes.deployTarget) {
        	execute "mkdir -p ${archiveDir}"
        	execute "mkdir -p ${deployDir}"
            put "build/distributions/${archive.archiveName}", archiveDir
            execute "cp -R ${deployDir} ${deployDir}.bak"
            execute "rm -rf ${deployDir}"
            execute "unzip ${archiveDir}/${archive.archiveName} -d ${deployDir}"
            execute "rm -rf ${deployDir}.bak"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.9'
}
